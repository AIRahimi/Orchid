apply plugin: 'java-library'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    testImplementation 'org.testng:testng:6.11'

    // Dynamic Component Registration
    api 'com.google.inject:guice:4.1.0'
    api 'com.google.inject.extensions:guice-multibindings:4.1.0'
    api 'io.github.lukehutch:fast-classpath-scanner:2.8.2'

    // core utilities
    api 'com.github.JavaEden:Common:v1.0.0'
    api 'com.github.JavaEden:Clog:v1.6.1'
    api 'com.github.JavaEden:Clog4j:v1.6.1'
    api 'org.projectlombok:lombok:1.16.18'
    api 'com.squareup.okhttp3:okhttp:3.9.0'
    api 'commons-io:commons-io:2.6'

    // Included parsers: JSON, YAML, TOML, CSV, Twig, Markdown, Sass
    api 'org.json:json:20170516'
    api 'org.yaml:snakeyaml:1.19'
    api 'com.moandjiezana.toml:toml4j:0.7.2'
    api 'org.jtwig:jtwig-core:5.86.1.RELEASE'
    api 'com.vladsch.flexmark:flexmark-all:0.28.0'
    api 'io.bit3:jsass:5.5.4'
    api 'com.univocity:univocity-parsers:2.5.8'

    // server
    api 'org.nanohttpd:nanohttpd:2.3.1'
    api 'org.nanohttpd:nanohttpd-websocket:2.3.1'

    // generate own documentation with Orchid
    orchidDocsRuntime rootProject.ext.mainProjects
}

static boolean otherProjectsTask(Task task, String taskName) {
    return !task.path.startsWith(':OrchidCore') && task.path.endsWith(":${taskName}")
}

project.parent.subprojects.each { subproject ->
    project.tasks.javadoc.dependsOn subproject.tasks.matching { task -> otherProjectsTask(task, 'jar') }
    project.tasks.orchidRun.dependsOn subproject.tasks.matching { task -> otherProjectsTask(task, 'jar') }

    project.tasks.javadoc.shouldRunAfter subproject.tasks.matching { task -> otherProjectsTask(task, 'javadoc') }
}

// Generate a Class containing the current build version, so it can be injected into OrchidSite
task generateSources {
    def outputDir = file("$buildDir/generated-src")
    outputs.dir outputDir
    doFirst {
        def srcFile = new File(outputDir, "com/eden/orchid/OrchidVersion.java")
        srcFile.parentFile.mkdirs()
        srcFile.write("""
package com.eden.orchid;
public class OrchidVersion {
   public static String getVersion() { return "$project.version"; }
}
""")
    }
}
compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java