
orchid {
    runTask = "${project.ext.commonRunTask}"
    version = "${project.version}"
    theme   = "${project.ext.commonTheme}"

    if(System.getenv('JITPACK')) {
        def jitpackBaseUrl = 'https://jitpack.io/com/github/JavaEden/Orchid'
        def commitHash = System.getenv('VERSION')
        baseUrl = "${jitpackBaseUrl}/${project.name}/${commitHash}/javadoc"
    }
    else if(project.hasProperty('env')) {
        switch(project.property('env')) {
            case 'prod':
                baseUrl = "${project.ext.commonBaseUrl}/${project.version}/${project.name}"
                break
            case 'staging':
                baseUrl = "${project.ext.commonBaseUrl}/${project.version}/${project.name}"
                break
            case 'dev':
                baseUrl = "${project.ext.commonBaseUrl}"
                break
        }
    }
    else {
        baseUrl = "${project.ext.commonBaseUrl}"
    }

    args = [
        "absApiKey ${project.properties['ABS_ApiKey']}"
    ]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from project.sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task prepareDocumentation {
    dependsOn 'javadoc'

    def source       = file("$buildDir/docs/javadoc")
    def dest_latest  = file("$rootDir/docs/latest/${project.name}")
    def dest_version = file("$rootDir/docs/${project.version}/${project.name}")

    inputs.dir(source)
    outputs.dir(dest_latest)
    outputs.dir(dest_version)

    doLast {
        copy {
            from source
            into dest_latest
        }
        copy {
            from source
            into dest_version
        }
    }
}

rootProject.tasks.prepareDocumentation.dependsOn project.tasks.prepareDocumentation
