apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.eden.orchidPlugin'
apply plugin: "com.dorongold.task-tree"
apply plugin: "org.ajoberstar.grgit"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

project.group 'io.github.javaeden.orchid'
project.version = rootProject.version

ext {
    if (project.hasProperty('theme') && project.property('theme') == 'Editorial') {
        commonTheme = ':themes:OrchidEditorial'
        commonThemeClass = 'com.eden.orchid.editorial.EditorialTheme'
    }
    else if (project.hasProperty('theme') && project.property('theme') == 'BsDoc') {
        commonTheme = ':themes:OrchidBsDoc'
        commonThemeClass = 'com.eden.orchid.bsdoc.BSDocTheme'
    }
    else {
        commonTheme = ':themes:OrchidMaterialize'
        commonThemeClass = 'com.eden.orchid.materialize.MaterializeTheme'
//        commonTheme = ':themes:OrchidEditorial'
//        commonThemeClass = 'com.eden.orchid.editorial.EditorialTheme'
    }

    commonRunTask = "listOptions"

    if (project.hasProperty('env')) {
        switch (project.property('env')) {
            case 'prod':
                commonBaseUrl = 'https://javaeden.github.io/Orchid'
                break
            case 'staging':
                commonBaseUrl = 'http://localhost:8080'
                break
            case 'dev':
                commonBaseUrl = 'http://localhost:8080'
                break
        }
    }
    else {
        commonBaseUrl = 'http://localhost:8080'
    }
}

apply from: "$rootDir/gradle/javadoc.gradle"
apply from: "$rootDir/gradle/publish.gradle"

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.+'
}