apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

afterEvaluate { project ->
    def pomConfig = {
        scm {
            url 'https://github.com/JavaEden/Orchid.git'
            connection 'https://github.com/JavaEden/Orchid.git'
            developerConnection 'https://github.com/JavaEden/Orchid.git'
        }
        licenses {
            license {
                name 'MIT'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'JavaEden'
                name 'Casey Brooks'
                email 'cjbrooks12@gmail.com'
            }
        }
    }

    project.publishing {
        publications {
            OrchidPublication(MavenPublication) {
                from project.components.java
                artifact project.sourcesJar
                artifact project.javadocJar
                groupId "${project.group}"
                artifactId "Orchid${project.name}"
                version "${project.version}"
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'A modern, extensible, theme-ready Javadoc site generator')
                    root.appendNode('name', 'Orchid')
                    root.appendNode('url', 'https://javaeden.github.io/Orchid/latest/core/')
                    root.children().last() + pomConfig
                }
            }
        }
    }


    bintray {
        user = "${project.properties['bintray_username']}"
        key = "${project.properties['bintray_apiKey']}"
        publications = ['OrchidPublication']

        dryRun = false
        publish = true
        override = true

        pkg {
            repo = 'Orchid'
            name = "Orchid${project.name}"
            userOrg = 'javaeden'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/JavaEden/Orchid.git'

            version {
                name = "${project.version}"
                desc = "Orchid ${project.name} ${project.version}"
                released = new Date()

                gpg {
                    sign = true
                }
                mavenCentralSync {
                    sync = true
                    user = "${project.properties['mavenCentral_username']}"
                    password = "${project.properties['mavenCentral_password']}"
                }
            }
        }
    }
}

task publishDocumentation {
    dependsOn 'javadoc'

    def source       = file("$buildDir/docs/javadoc")
    def dest_latest  = file("$rootDir/docs/latest/${project.name}")
    def dest_version = file("$rootDir/docs/${project.version}/${project.name}")

    inputs.dir(source)
    outputs.dir(dest_latest)
    outputs.dir(dest_version)

    doLast {
        copy {
            from source
            into dest_latest
        }
        copy {
            from source
            into dest_version
        }
    }
}

project.tasks.publish.dependsOn publishDocumentation
project.tasks.publish.dependsOn bintrayUpload

rootProject.tasks.deploy.dependsOn project.tasks.publish