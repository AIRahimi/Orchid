
dependencies {
    // Dynamic Component Registration
    compile 'com.google.inject:guice:4.1.0'
    compile 'com.google.inject.extensions:guice-multibindings:4.1.0'
    compile 'io.github.lukehutch:fast-classpath-scanner:2.0.17'

    // core utilities
    compile 'com.github.JavaEden:Common:74714dca1f'
    compile 'com.github.JavaEden:Clog4j:v1.3.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'org.projectlombok:lombok:1.16.14'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'fr.opensagres.js:minimatch.java:1.1.0'

    // Included parsers: JSON, YAML, CSV, Twig, Markdown
    compile 'org.json:json:20160810'
    compile 'org.yaml:snakeyaml:1.17'
    compile 'com.univocity:univocity-parsers:2.3.1'
    compile 'org.jtwig:jtwig-core:5.75'
    compile 'com.vladsch.flexmark:flexmark:0.14.0'
    compile 'org.asciidoctor:asciidoctorj:1.5.4'
    compile 'io.bit3:jsass:5.4.3'

    // generate own documentation with Orchid
    orchidDocsCompile rootProject.ext.mainProjects
}

static boolean otherProjectsTask(Task task, String taskName) {
    return !task.path.startsWith(':OrchidCore') && task.path.endsWith(":${taskName}")
}

project.parent.subprojects.each { subproject ->
    project.tasks.javadoc.dependsOn subproject.tasks.matching { task -> otherProjectsTask(task, 'jar') }
    project.tasks.orchidRun.dependsOn subproject.tasks.matching { task -> otherProjectsTask(task, 'jar') }

    project.tasks.javadoc.shouldRunAfter subproject.tasks.matching { task -> otherProjectsTask(task, 'javadoc') }
}